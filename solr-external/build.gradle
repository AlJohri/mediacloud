// To start ZooKeeper for managing Solr shards, run:
//
//    gradle runZooKeeper
//
// To start Solr, run:
//
//     gradle runSolr [-Dmediacloud.solr.shardToStart=1 -Dmediacloud.solr.numShards=1]
//

import org.gradle.api.plugins.jetty.JettyRunWar
import org.apache.commons.io.FileUtils

buildscript {
  repositories {
      mavenCentral()
  }
    dependencies {
      classpath 'org.apache.commons:commons-io:1.3.2'
  }
}


apply plugin: 'java'
apply plugin: 'jetty'

ext {
    // Port to listen to
    // (when starting shards, this is going to be a starting port, i.e.
    // shard #1 will start on port listenPort, shard #2 on port listenPort+1
    // and so on)
    listenPort = 8983

    // ZooKeeper host and port
    zooKeeperHost = "localhost"
    zooKeeperPort = 9983

    // Solr home directory (relative to build.gradle)
    solrHomeDir = "./mediacloud"

    // Solr data directory (relative to build.gradle)
    solrDataDir = "./../data/solr"

    // ZooKeeper data directory (relative to build.gradle)
    zooKeeperDataDir = "./../data/solr-zookeeper"

    // Bootstrap collection name for ZooKeeper
    zooKeeperBootstrapCollection = "collection1"

    // Zookeeper config name
    zooKeeperConfigName = "mediacloud"

    // Lucene match version ("controls what version of Lucene various
    // components of Solr adhere to"), e.g. "LUCENE_46" or "4.10.3"
    //
    // This value is stored in Solr distribution's example "solrconfig.xml",
    // under "<luceneMatchVersion>" element.
    luceneMatchVersion = "LUCENE_46"

    // Package versions
    solrVersion = "4.6.0"
    zooKeeperVersion = "3.4.8"
    postgresqlVersion = "9.3-1104-jdbc41"
    slf4jSimpleVersion = "1.7.12"
    commonsLoggingVersion = "1.2"
    jUnitVersion = "4.12"
}

/* ------------------------------------------------ */

ext.getShardToStart = {
    final String shardToStartProperty = "mediacloud.solr.shardToStart"
    String stringShardToStart = System.getProperty(shardToStartProperty)

    if (stringShardToStart == null) {
        logger.info("'" + shardToStartProperty + "' is not set, defaulting to shard 1")
        return 1
    } else {
        return Integer.parseInt(stringShardToStart)
    }
}

ext.getNumShards = {
    final String numShardsProperty = "mediacloud.solr.numShards"
    String stringNumShards = System.getProperty(numShardsProperty)

    if (stringNumShards == null) {
        logger.info("'" + numShardsProperty + "' is not set, defaulting to 1 shard")
        return 1
    } else {
        return Integer.parseInt(stringNumShards)
    }
}

ext.getShardPort = {
    int shardToStart = getShardToStart()
    if (shardToStart > 1) {
        return listenPort + shardToStart - 1
    } else {
        return listenPort
    }
}

repositories {
    mavenCentral()
    maven {
        // for org.restlet.jee:org.restlet dependency
        url "http://maven.restlet.org"
    }
}

// custom configuration for running the webapp
configurations {
    zooKeeperJars
    solrWebApp
    additionalSolrJars
    additionalJettyLoggingJars
}

dependencies {
    // External ZooKeeper for managing Solr shards
    zooKeeperJars "org.apache.zookeeper:zookeeper:${zooKeeperVersion}"

    // Solr
    solrWebApp "org.apache.solr:solr:${solrVersion}@war"

    // Additional Solr JARs
    additionalSolrJars "org.apache.solr:solr-analysis-extras:${solrVersion}"
    additionalSolrJars "org.apache.solr:solr-cell:${solrVersion}"
    additionalSolrJars "org.apache.solr:solr-clustering:${solrVersion}"
    additionalSolrJars "org.apache.solr:solr-dataimporthandler:${solrVersion}"
    additionalSolrJars "org.apache.solr:solr-dataimporthandler-extras:${solrVersion}"
    additionalSolrJars "org.apache.solr:solr-langid:${solrVersion}"
    additionalSolrJars "org.apache.solr:solr-solrj:${solrVersion}"
    additionalSolrJars "org.apache.solr:solr-uima:${solrVersion}"
    additionalSolrJars "org.apache.solr:solr-velocity:${solrVersion}"
    additionalSolrJars "org.postgresql:postgresql:${postgresqlVersion}"

    // Jetty's JARs for logging
    additionalJettyLoggingJars "org.slf4j:slf4j-simple:${slf4jSimpleVersion}"
    additionalJettyLoggingJars "commons-logging:commons-logging:${commonsLoggingVersion}"

    testCompile "junit:junit:${jUnitVersion}"
    testCompile "org.apache.solr:solr-test-framework:${solrVersion}"
    testCompile "org.slf4j:slf4j-simple:${slf4jSimpleVersion}"
    testCompile "commons-logging:commons-logging:${commonsLoggingVersion}"
}

// Destination directory
buildDir = "solr-dist"

// Where to look for web app
project.webAppDirName = "solr-dist"

// Web application directory
def explodedWebAppDir = new File("${buildDir}/explodedWebAppDir").getAbsolutePath()
logger.debug("explodedWebAppDir: " + explodedWebAppDir)

// ZooKeeper class directory
def zooKeeperClassDir = new File("${buildDir}/zooKeeperClassDir").getAbsolutePath()
logger.debug("zooKeeperClassDir: " + zooKeeperClassDir)

/* ------------------------------------------------ */

task copyZooKeeperJars(type: Copy) {
    from configurations.zooKeeperJars
    into zooKeeperClassDir
}

task copyWar(type: Copy) {
    from zipTree(configurations.solrWebApp.singleFile)
    into explodedWebAppDir 
}

task copyAdditionalSolrJars(type: Copy) {
    from configurations.additionalSolrJars
    into explodedWebAppDir + "/WEB-INF/lib/"
}

task copyAdditionalJettyLoggingJars(type: Copy) {
    from configurations.additionalJettyLoggingJars
    into explodedWebAppDir + "/WEB-INF/lib/"
}

task zooKeeperUpConfig(type: JavaExec, dependsOn: ['copyZooKeeperJars']) {
    main = "org.apache.solr.cloud.ZkCLI"
    classpath = files(
        zooKeeperClassDir + "/*",
        explodedWebAppDir + "/WEB-INF/lib/*",
    )
    args = [
        "-zkhost", zooKeeperHost + ":" + Integer.toString(zooKeeperPort),
        "-cmd", "upconfig",
        "-confname", zooKeeperConfigName,
        "-confdir", solrHomeDir + "/collections/" + zooKeeperBootstrapCollection + "/conf",
    ]
}

task zooKeeperLinkConfig(type: JavaExec, dependsOn: ['copyZooKeeperJars', 'zooKeeperUpConfig']) {
    main = "org.apache.solr.cloud.ZkCLI"
    classpath = files(
        zooKeeperClassDir + "/*",
        explodedWebAppDir + "/WEB-INF/lib/*",
    )
    args = [
        "-zkhost", zooKeeperHost + ":" + Integer.toString(zooKeeperPort),
        "-cmd", "linkconfig",
        "-collection", zooKeeperBootstrapCollection,
        "-confname", zooKeeperConfigName,
        "-solrhome", solrHomeDir,
    ]
}

task zooKeeperBootstrap(type: JavaExec, dependsOn: ['copyZooKeeperJars', 'zooKeeperLinkConfig']) {
    main = "org.apache.solr.cloud.ZkCLI"
    classpath = files(
        zooKeeperClassDir + "/*",
        explodedWebAppDir + "/WEB-INF/lib/*",
    )
    args = [
        "-zkhost", zooKeeperHost + ":" + Integer.toString(zooKeeperPort),
        "-cmd", "bootstrap",
        "-solrhome", solrHomeDir,
    ]
}

task createOrUpdateShard << {
    Integer shardToStart = getShardToStart()

    File solrHomeDirFile = new File(solrHomeDir)
    String shardDir = solrDataDir + "/mediacloud-shard-" + shardToStart
    File shardDirFile = new File(shardDir)

    logger.info("Updating shard " + shardToStart + " at " + shardDirFile + "...")

    // Automagically resolves symlinks
    FileUtils.copyDirectory(solrHomeDirFile, shardDirFile)

    // Remove template directory
    File baseCollectionDir = new File(shardDir + "/collections/base_collection")
    if (baseCollectionDir.exists()) {
        FileUtils.deleteDirectory(baseCollectionDir)
    } else {
        // Unexpectedly missing, maybe got renamed?
        logger.warn("'base_collection' directory does not exist at " + baseCollectionDir)
    }
}

// Prepare for running Solr
task setSolrProperties(dependsOn: ['createOrUpdateShard', 'zooKeeperBootstrap']) << {

    Integer shardToStart = getShardToStart()
    Integer numShards = getNumShards()

    String shardSolrHomeDir = solrDataDir + "/mediacloud-shard-" + shardToStart
    if (! new File(shardSolrHomeDir).exists()) {
        throw new GradleException("'solr.solr.home' for shard " + shardToStart + " does not exist at " + shardSolrHomeDir)
    }

    String zooKeeperHostAndPort = zooKeeperHost + ":" + Integer.toString(zooKeeperPort)

    // Solr properties
    System.setProperty("zkHost", zooKeeperHostAndPort)
    System.setProperty("numShards", Integer.toString(numShards))
    System.setProperty("host", "mediacloud-shard-" + Integer.toString(shardToStart))
    System.setProperty("solr.solr.home", shardSolrHomeDir)
    System.setProperty("mediacloud.luceneMatchVersion", luceneMatchVersion)

    // Jetty properties
    System.setProperty("org.eclipse.jetty.server.Request.maxFormContentSize", Integer.toString(4 * 1024 * 1024))
}

// Run ZooKeeper (will be bootstrapped before starting a shard)
task runZooKeeper(type: JavaExec, dependsOn: ['copyZooKeeperJars']) {
    main = "org.apache.zookeeper.server.quorum.QuorumPeerMain"
    classpath = files(zooKeeperClassDir + "/*")
    args = [
        Integer.toString(zooKeeperPort),
        zooKeeperDataDir
    ]
    systemProperties = [
        "zookeeper.log.dir": zooKeeperDataDir
    ]
}

// Run Solr
// FIXME dependency tasks don't get always run
task runSolr(type: JettyRun, dependsOn: [
    'copyWar',
    'copyAdditionalSolrJars',
    'copyAdditionalJettyLoggingJars',
    'createOrUpdateShard',
    'setSolrProperties',
]) {
    httpPort = getShardPort()
    contextPath = 'solr'
    webAppSourceDirectory = file(explodedWebAppDir)
}
